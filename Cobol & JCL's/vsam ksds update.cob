IDENTIFICATION DIVISION.
PROGRAM-ID. VSAMUPDT.
ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT EMPLOYEE-MASTER ASSIGN TO 'EMPMAST.DAT'
    ORGANIZATION IS INDEXED
    ACCESS MODE  IS RANDOM
    RECORD KEY   IS EMP-ID.
    SELECT TRANSACTION-FILE ASSIGN TO 'TRANFILE.DAT'
    ORGANIZATION IS SEQUENTIAL
    ACCESS MODE  IS SEQUENTIAL.
    SELECT ERROR-FILE ASSIGN TO 'ERRFILE.DAT'
    ORGANIZATION IS SEQUENTIAL
    ACCESS MODE  IS SEQUENTIAL.
DATA DIVISION.
FILE SECTION.
FD  EMPLOYEE-MASTER
    RECORD CONTAINS 50 CHARACTERS.
01  MASTER-RECORD.
    03  EMP-ID       PIC X(05).
    03  EMP-NAME     PIC X(15).
    03  EMP-LOC      PIC X(03).
    03  EMP-TECH     PIC X(05).
    03  EMP-DOB      PIC 9(08).
    03  EMP-EARN     PIC 9(5)V99.
    03  EMP-DEDN     PIC 9(5)V99.
FD  TRANSACTION-FILE
    RECORD CONTAINS 51 CHARACTERS.
01  TRANSACTION-RECORD.
    03  T-ACTION-CODE PIC X(01).
     88  ADD-EMP     VALUE 'A'.
     88  UPD-EMP     VALUE 'U'.
     88  DEL-EMP     VALUE 'D'.
    03  T-DATA.
     05  T-EMP-ID    PIC X(05).
     05  T-EMP-NAME  PIC X(15).
     05  T-EMP-LOC   PIC X(03).
     05  T-EMP-TECH  PIC X(05).
     05  T-EMP-DOB   PIC 9(08).
     05  T-EMP-EARN  PIC 9(5)V99.
     05  T-EMP-DEDN  PIC 9(5)V99.
FD  ERROR-FILE
    RECORD CONTAINS 80 CHARACTERS.
01  ERROR-RECORD.
    03  ERR-REC      PIC X(51).
    03  ERR-MSG      PIC X(29).
WORKING-STORAGE SECTION.
01  WS-FILE-FLAG     PIC X(01) VALUE 'N'.
    88  END-OF-FILE          VALUE 'Y'.
01  WS-EMP-FLAG      PIC X(01) VALUE 'N'.
    88  EMP-FOUND            VALUE 'Y'.
01  TOTAL-COUNTERS.
    03  RECS-READ    PIC 9(02) VALUE ZERO.
    03  RECS-ADDED   PIC 9(02) VALUE ZERO.
    03  RECS-UPDATED PIC 9(02) VALUE ZERO.
    03  RECS-DELETED PIC 9(02) VALUE ZERO.
    03  RECS-ERROR   PIC 9(02) VALUE ZERO.
PROCEDURE DIVISION.
MAIN-PARA.
    PERFORM INIT-PARA
    PERFORM PROCESS-PARA UNTIL END-OF-FILE
    PERFORM TERM-PARA
    STOP RUN.
INIT-PARA.
    OPEN INPUT  TRANSACTION-FILE
     I-O EMPLOYEE-MASTER
     OUTPUT ERROR-FILE.
    PERFORM READ-TRANSACTION.
READ-TRANSACTION.
    READ TRANSACTION-FILE
     AT END
        MOVE 'Y' TO WS-FILE-FLAG
     NOT AT END
        ADD 1 TO RECS-READ
    END-READ.
READ-EMPLOYEE.
    MOVE T-EMP-ID TO EMP-ID
    READ EMPLOYEE-MASTER
     INVALID KEY
        MOVE 'N' TO WS-EMP-FLAG
     NOT INVALID KEY
        MOVE 'Y' TO WS-EMP-FLAG
    END-READ.
PROCESS-PARA.
    EVALUATE TRUE
     WHEN ADD-EMP
        PERFORM INSERT-PARA
     WHEN UPD-EMP
        PERFORM UPDATE-PARA
     WHEN DEL-EMP
        PERFORM DELETE-PARA
     WHEN OTHER
        MOVE 'INVALID ACTION CODE' TO ERR-MSG
        PERFORM ERROR-PARA
    END-EVALUATE
    PERFORM READ-TRANSACTION.
INSERT-PARA.
    PERFORM READ-EMPLOYEE
    IF EMP-FOUND
     MOVE 'EMP FOR INSERT EXISTS' TO ERR-MSG
     PERFORM ERROR-PARA
    ELSE
     PERFORM ADD-RECORD
    END-IF.
UPDATE-PARA.
    PERFORM READ-EMPLOYEE
    IF EMP-FOUND
     PERFORM UPDATE-RECORD
    ELSE
     MOVE 'EMP FOR UPDATE NOT EXIST' TO ERR-MSG
     PERFORM ERROR-PARA
    END-IF.
DELETE-PARA.
    PERFORM READ-EMPLOYEE
    IF EMP-FOUND
     PERFORM DELETE-RECORD
    ELSE
     MOVE 'EMP FOR DELETE NOT EXIST' TO ERR-MSG
     PERFORM ERROR-PARA
    END-IF.
ERROR-PARA.
    MOVE TRANSACTION-RECORD TO ERR-REC
    WRITE ERROR-RECORD
    ADD 1 TO RECS-ERROR.
ADD-RECORD.
    WRITE MASTER-RECORD FROM T-DATA
     INVALID KEY
        DISPLAY 'DUPLICATE RECORD EXISTS'
        MOVE 'EMP FOR INSERT EXISTS' TO ERR-MSG
        PERFORM ERROR-PARA
     NOT INVALID KEY
        ADD 1 TO RECS-ADDED
    END-WRITE.
UPDATE-RECORD.
    REWRITE MASTER-RECORD FROM T-DATA
     INVALID KEY
        DISPLAY 'RECORD DOES NOT EXIST'
        MOVE 'EMP FOR UPDATE DOES NOT EXIST' TO ERR-MSG
        PERFORM ERROR-PARA
     NOT INVALID KEY
        ADD 1 TO RECS-UPDATED
    END-REWRITE.
DELETE-RECORD.
    MOVE T-EMP-ID TO EMP-ID
    DELETE EMPLOYEE-MASTER
     INVALID KEY
        DISPLAY 'RECORD DOES NOT EXIST'
        MOVE 'EMP FOR DELETE DOES NOT EXIST' TO ERR-MSG
        PERFORM ERROR-PARA
     NOT INVALID KEY
       ADD 1 TO RECS-DELETED
    END-DELETE.
TERM-PARA.
    DISPLAY '** END OF PROGRAM TOTALS **'
    DISPLAY 'RECS READ    ', RECS-READ
    DISPLAY 'RECS ADDED   ', RECS-ADDED
    DISPLAY 'RECS UPDATED ', RECS-UDPATED
    DISPLAY 'RECS DELETED ', RECS-DELETED
    DISPLAY 'RECS ERROR   ', RECS-ERROR
    DISPLAY '** --------------------- **'
    CLOSE TRANSACTION-FILE,
       EMPLOYEE-MASTER,
       ERROR-FILE.
